group 'com.onesignal.flutter'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compileOnly 'com.google.android.gms:play-services-gcm:12.0.1'
    api 'com.google.firebase:firebase-messaging:12.0.1'
    // Can be compileOnly in 4.0.0, can't change until then as some projects may rely on this
    implementation 'com.google.android.gms:play-services-location:12.0.1'
    // Required for GoogleApiAvailability
    implementation 'com.google.android.gms:play-services-base:12.0.1'
    api 'com.android.support:support-v4:27.1.1'
    api 'com.android.support:customtabs:27.1.1'
    api fileTree(include: ['*.aar'], dir: 'libs')
}

// Adds required manifestPlaceholders keys to allow mainifest merge gradle step to complete
// The OneSignal app id should be set in your JS code.
// Google project number / FCM Sender ID will be pulled in from the OneSignal dashbaord
class DefaultManifestPlaceHolders {
    static final MANIFEST_PLACEHOLDERS_DEFAULTS = [
        onesignal_app_id: '',
        onesignal_google_project_number: 'REMOTE'
    ]

    static void addManifestToAppProject(Project proj) {
        def androidApp = proj.android
        MANIFEST_PLACEHOLDERS_DEFAULTS.each { defKey, defValue ->
            if (!androidApp.defaultConfig.manifestPlaceholders.containsKey(defKey))
                androidApp.defaultConfig.manifestPlaceholders[defKey] = defValue

            androidApp.buildTypes.each { buildType ->
                if (!buildType.manifestPlaceholders.containsKey(defKey))
                    buildType.manifestPlaceholders[defKey] = defValue
            }
        }
    }
}

rootProject.childProjects.each { projName, proj ->
    if (projName != 'app')
        return

    if (proj.hasProperty('android')) {
        DefaultManifestPlaceHolders.addManifestToAppProject(proj)
        return
    }

    proj.afterEvaluate {
        DefaultManifestPlaceHolders.addManifestToAppProject(proj)
    }
}